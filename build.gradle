plugins {
    id 'distribution'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.jfrog.org/artifactory/jfrog-dependencies"
    }
}

group = "io.pravega"
version = releaseVersion

description = "Boomi Pravega Connector"
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    localDeps
}

dependencies {
    compile "io.pravega:pravega-client:${pravegaVersion}"
    compile "com.jayway.jsonpath:json-path:${jsonPathVersion}"
    localDeps "com.boomi:connector-sdk-util:${boomiSdkVersion}"
    compileOnly "com.boomi:connector-sdk-api:${boomiSdkVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
    testCompile "io.pravega:pravega-standalone:${pravegaVersion}"
    testCompile "com.boomi:connector-sdk-api:${boomiSdkVersion}"
    testCompile "com.boomi:connector-sdk-test-util:${boomiSdkVersion}"
    testCompile "org.slf4j:slf4j-api:1.7.9"
    testCompile "ch.qos.logback:logback-core:1.2.3"
    testRuntime "ch.qos.logback:logback-classic:1.2.3"
    compile configurations.localDeps
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task dependencyJar(type: ShadowJar) {
    classifier = 'dependencies'
    configurations = [project.configurations.runtime]
    manifest {
        attributes 'Implementation-Version': project.version
    }
    mergeServiceFiles()
    dependencies {
        exclude(dependency('com.boomi:connector-sdk-util'))
        exclude(dependency('io.netty:netty-tcnative-boringssl-static'))
    }
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('META-INF') {
                from(project.file('src/main/resources/connector-config.xml'))
            }
            into('lib') {
                from configurations.localDeps
                from jar
            }
            into '/'
        }
    }
}

test {
    useJUnitPlatform()
    systemProperties 'singlenode.configurationFile': project.file('src/test/resources/standalone-config.properties')
}
