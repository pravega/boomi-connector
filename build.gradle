import com.github.jk1.license.render.*
plugins {
    id 'distribution'
    id 'java'
    id 'com.github.jk1.dependency-license-report' version '1.13'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.jfrog.org/jfrog-dependencies"
    }
}

group = "io.pravega"
version = releaseVersion

description = "Boomi Pravega Connector"
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("io.pravega:pravega-client:${pravegaVersion}") {
        exclude group: "io.netty", module: "netty-tcnative-boringssl-static"
    }
    compile "io.pravega:pravega-keycloak-client:${pravegaKeycloakVersion}"
    compile "com.jayway.jsonpath:json-path:${jsonPathVersion}"
    compile "com.boomi:connector-sdk-util:${boomiConnectorVersion}"
    compileOnly "com.boomi:common-sdk:${boomiCommonSdkVersion}"
    compileOnly "com.boomi:connector-sdk-api:${boomiConnectorVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
    testCompile "io.pravega:pravega-standalone:${pravegaVersion}"
    compile "com.boomi:boomi-util:${boomiUtilVersion}"
    testCompile "com.boomi:common-sdk:${boomiCommonSdkVersion}"
    testCompile "com.boomi:connector-sdk-api:${boomiConnectorVersion}"
    testCompile "com.boomi:connector-sdk-test-util:${boomiConnectorVersion}"
    testCompile "org.slf4j:slf4j-api:1.7.9"
    testCompile "ch.qos.logback:logback-core:1.2.3"
    testRuntime "ch.qos.logback:logback-classic:1.2.3"
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('META-INF') {
                from(project.file('src/main/resources/connector-config.xml'))
            }
            into('lib') {
                from configurations.runtime
                from jar
            }
            into '/'
        }
    }
}

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$projectDir/3rd-party-licenses"

    // Adjust the configurations to fetch dependencies, e.g. for Android projects. Default is 'runtimeClasspath'
    configurations = ['runtime']
    renderers = [new InventoryHtmlReportRenderer()]

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true
}

test {
    useJUnitPlatform()
    systemProperties 'singlenode.configurationFile': project.file('src/test/resources/standalone-config.properties')
}
