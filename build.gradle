plugins {
    id 'distribution'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "io.freefair.lombok" version "4.1.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.jfrog.org/artifactory/jfrog-dependencies"
    }
}

group = "io.pravega"
version = releaseVersion

description = "Boomi Pravega Connector"
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "io.pravega:pravega-client:${pravegaVersion}"
    compile "org.json:json:${jsonVersion}"
    compile "com.boomi:connector-sdk-util:${boomiSdkVersion}"
    compileOnly "com.boomi:connector-sdk-api:${boomiSdkVersion}"
    testCompile "com.boomi:connector-sdk-test-util:${boomiSdkVersion}"
}

shadowJar {
    minimize() {
        // exclude dependencies here to re-add them if they are mistakenly removed
    }
    append('META-INF/connector-config')
    manifest {
        attributes 'Implementation-Version': project.version
    }
    mergeServiceFiles()
    dependencies {
        exclude(dependency('io.netty:netty-tcnative-boringssl-static'))
    }
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            into('META-INF') {
                from(project.file('src/main/resources/connector-config.xml'))
            }
            into('lib') {
                from shadowJar
            }
        }
    }
}
