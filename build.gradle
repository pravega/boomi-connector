plugins {
    id 'java'
    id 'distribution'
    id 'com.github.jk1.dependency-license-report' version '1.13'
}

repositories {
    mavenCentral()
    maven {
        name = "Connector SDK Repository"
        url = "https://boomisdk.s3.amazonaws.com/releases"
    }
}

group = "io.pravega"
description = "Boomi Pravega Connector"
version = releaseVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "com.boomi.commonsdk:common-sdk:${boomiCommonSdkVersion}"
    compileOnly "com.boomi.connsdk:connector-sdk-api:${boomiConnectorVersion}"
    compileOnly "com.boomi.util:boomi-util:${boomiUtilVersion}"
    implementation "io.pravega:pravega-client:${pravegaVersion}"
    implementation "io.pravega:pravega-keycloak-client:${pravegaKeycloakVersion}"
    implementation "com.jayway.jsonpath:json-path:${jsonPathVersion}"
    implementation("com.boomi.connsdk:connector-sdk-util:${boomiConnectorVersion}") {
        // must exclude these from the distribution
        exclude group: 'com.boomi.commonsdk', module: 'common-sdk'
        exclude group: 'com.boomi.connsdk', module: 'connector-sdk-api'
        exclude group: 'com.boomi.util', module: 'boomi-util'
    }
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "io.pravega:pravega-standalone:${pravegaVersion}"
    testImplementation "com.boomi.connsdk:connector-sdk-test-util:${boomiConnectorVersion}"
    testImplementation "org.slf4j:slf4j-api:1.7.9"
    testImplementation "ch.qos.logback:logback-core:1.2.3"
    testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testRuntimeOnly "com.google.guava:guava:31.1-jre"
}

distributions {
    main {
        contents {
            into('META-INF') {
                from(project.file('src/main/resources/connector-config.xml'))
            }
            into('lib') {
                from configurations.runtimeClasspath
                from jar
            }

            // Exclude the jars that are available in the atom or those that are required for testing
            exclude("bcprov-ext-jdk15on-1.70.jar")
            exclude("guava-*.jar")
            exclude("slf4j*.jar")
            into '/'
        }
    }
}


import com.github.jk1.license.render.InventoryHtmlReportRenderer

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$projectDir/3rd-party-licenses"

    // Adjust the configurations to fetch dependencies, e.g. for Android projects. Default is 'runtimeClasspath'
    configurations = ['runtime']
    renderers = [new InventoryHtmlReportRenderer()]

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true
}

test {
    useJUnitPlatform()
    systemProperties 'singlenode.configurationFile': project.file('src/test/resources/standalone-config.properties')

    // Adjust heap size for running local pravega emulator during testing
    minHeapSize = "512m" // initial heap size
    maxHeapSize = "16384m" // maximum heap size
}
